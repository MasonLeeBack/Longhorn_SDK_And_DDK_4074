'******************************* TCP_ZeroWindow_ProbeRetry.wsf ***********************
'*                                                                                   *
'* Author            :    Sandeep Prabhu                                             *
'* Revision history  :                                                               *
'*    09/20/2000     sandeep        Created                                          *
'*    04/24/2003     karolys        added HCT-style comments for tst plan generation *
'*                                                                                   *
'* This script will test the TCP zero window probing screnarios                      *
'*                                                                                   *
'******************************* TCP_ZeroWindow_ProbeRetry.wsf ***********************


<package>
   <job id="Job 1">
      <reference id="Sparta" object="Spartacom.base.1" version="1.0"/>
      <reference id="Core" object="Testcore.base.1" version="1.0"/>
      <reference id="AutoSrv" object="AutoSrvCom.base.1" version="1.0"/>
      <script language="VBScript" src="..\..\common\InitAddress.vbs"/>
      <script language="VBScript" src="..\TCPLib.vbs"/>
      <script language="VBScript" src="TCPZeroWindowLib.vbs"/>                  
      <script id="TCP_ZeroWindow_ProbeRetry" language="VBScript">

'/**
' @area name="Offload Compliance Test Assertions"
' @group name="TCP Zero Window Probing Assertions"
' @group name="Zero Window Probe Retries Assertions" 
' @define name="Offload" Offload Compliance Test Assertions
' @define name="TCPZW" TCP Zero Window Probing Assertions
' @define name="RetZW" Zero Window Probe Retries Assertions
' @hierarchy Offload TCPZW RetZW
' @key Reference="WLP# - x.x.x.x; RFC-793 Section 3.7, RFC-1122 Section 4.2.2.17"
' @key TestDesc="TD-x.xx Offload Compliance Test Description"
' @key TestName="TCPv4 Zero Window Probe Retry Count Tests"
' @key TestLog="TCP_ZeroWindow_ProbeRetry.log"
'*/

option explicit
    
Dim Core, Sparta, Autosrv, MediaType, AutoArpObject, IFace, pControllerObj
Const usMSS = 1460
 
'
' Start the script
'           
StartScript "TCP_ZeroWindow_ProbeRetry", g_szLogDirectory, "Tests for TCP zero window probing" 



' ==================================================================================================
' Main routine
' ==================================================================================================
Sub Main
   Core.SetAssertionTracking "60700"
   Dim ulStatus, ulWindowSize
   
   IFace.StartListening
   Core.StartTest "Zero window probing retries"

   '/**
   ' @test name="Verify that the stack sends the correct number of zero window probe retries if the other party doesn't respond." 
   ' Autosrv on the test machine is requested to establish a TCP connection back to the script machine. 
   ' The script accepts the connection and advertises a zero window in its SYN-ACK.
   ' The stack should send a zero window probe and then retransmit the packet 5 times when it doesn't get a response.
   ' At the end the script resets the connection.<br/>
   '*/
   
   ZeroWindowProbeRetries
   Core.EndTest
   Core.Sleep(2500)
   IFace.StopListening     
   
End Sub



'
' Routine to test the zero window probe intervals
'
Sub ZeroWindowProbeRetries
   Dim ulStatus, ulSeqNumber, ulAckNumber, ulSessionId, ulTID, usSrcPort, usDestPort
   Dim ulLastAck, ulBytesRcvd, ulDataLength, pTCPPacket
   
   ulSeqNumber = Clng(1000)
   ulAckNumber = Clng(1)
   usSrcPort = 5000
   usDestPort = 5001
   
   Core.StartVariation

   
   '
   ' Esatablish a TCP connection and advertize a 0 window in SYN-ACK
   '
   ulStatus = Connect(usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId, 0, usMSS)
   if (ulStatus <> 0) then
      Core.BlockVariation "ZeroWindowProbeRetries: Connect - " & ulStatus
      exit sub
   end if
   
   '
   ' Ask Autosrv to start sending data
   '
   ulStatus = pControllerObj.Send(ulSessionId, 1, 0, usMSS, ulTID)
   if (ulStatus <> 0) then
      Core.BlockVariation "ZeroWindowProbeRetries: Send - " & ulStatus
      ResetConnection usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId
      exit sub
   end if
   
   '
   ' Start receiving the window probe packets and don't respond
   '
   Dim ulNumProbes, ulTimeout
   ulNumProbes = 0
   ulTimeout = 3500
   do
      set pTCPPacket = RecvTCPPacket(ulSeqNumber, ulAckNumber, ulDataLength, TCP_ACK, ulTimeout)
      if (pTCPPacket is Nothing) then
         exit do
      end if  
      if ((pTCPPacket.TCPHeader.Flag <> TCP_ACK) OR (ulDataLength <> 1)) then
         exit do
      end if
      ulNumProbes = ulNumProbes + 1
      if (ulNumProbes < 6) then
         ulTimeout = ulTimeout * 2
      end if
   loop while (TRUE)

   if (ulNumProbes <> DefaultTcpZeroProbeRetries) then
      Core.BlockVariation "ZeroWindowProbeRetries: # Zero probe retries = " & ulNumProbes
      ResetConnection usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId
      exit sub
   end if
   

   ' Reset the connection
   '
   ResetConnection usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId
End Sub



      </script>
   </job>
</package>
