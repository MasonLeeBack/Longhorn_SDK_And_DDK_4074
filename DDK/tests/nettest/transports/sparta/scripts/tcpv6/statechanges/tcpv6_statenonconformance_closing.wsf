'
' File          : TCPv6_StateNonConformance_CLOSING.wsf
' Author        : Sandeep Prabhu [5/14/01]
'
' A script to test the state non-conformance scenarios in CLOSING state
'

<package>
   <job id="Job 1">
      <reference id="Sparta" object="Spartacom.base.1" version="1.0"/>
      <reference id="Core" object="Testcore.base.1" version="1.0"/>
      <reference id="AutoSrv" object="AutoSrvCom.base.1" version="1.0"/>
      <script language="VBScript" src="..\..\common\InitAddress.vbs"/>
      <script language="VBScript" src="..\..\common\IPv6Common.Lib"/>
      <script language="VBScript" src="..\..\common\TCPv6Helper.Lib"/>
      <script language="VBScript" src="TCPv6StateChangesHelper.Lib"/>
      <script id="TCPv6_StateNonConformance_CLOSING" language="VBScript">


option explicit
    
Dim Core, Sparta, Autosrv, MediaType, AutoNDObject, IFace, pControllerObj, ExceptionStatus

 
StartScript "TCPv6_StateNonConformance_CLOSING", g_szLogDirectory, "Tests for state non conformance in CLOSING"


'
' Main routine
'
Sub Main

   Core.SetAssertionTracking "20300"
   '/**
   ' @area name="Offload Compliance Test Assertions"
   ' @group name="TCP Connection State Transition Assertions"
   ' @group name="State Nonconformance in CLOSING State Assertions for TCP/IP v6" 
   ' @define name="Offload" Offload Compliance Test Assertions
   ' @define name="TCPStates" TCP Connection State Transition Assertions
   ' @define name="StateNonConfv6" State Nonconformance in CLOSING State Assertions for TCP/IP v6
   ' @hierarchy Offload TCPStates StateNonConfv6
   ' @key Reference="WLP# - x.x.x.x; RFC-793 Sections 3.2, 3.4, 3.5, RFC-1122 Section 4.2.2.8, 4.2.2.12, 4.2.2.13"
   ' @key TestDesc="TD-x.xx Offload Compliance Test Description"
   ' @key TestName="TCPv6 Connection State Nonconformance in CLOSING Tests"
   ' @key TestLog="TCPv6_StateNonConformance_CLOSING.log"
   '*/
   Core.StartTest "CLOSING state"
   '/**
   ' @test name="The stack must repond correctly to the below scenarios when the TCP connection is in CLOSING state"
   ' Application on the test machine accepts a connection from the remote end.
   ' Test verifies that Stack transitions the connection to ESTABLISHED state. 
   ' Application closes the connection.
   ' Test verifies that Stack sends FIN and transitions the connection to FIN_WAIT1 state. 
   ' Upon stack receiving a FIN, test verifies that Stack sends an ACK and transitions the connection to CLOSING state. <br/>
   ' While the connection is at CLOSING state, <br/>
   ' - upon Stack receiving a RST, test verifies that Stack transitions the connection to CLOSED state. <br/>
   ' - upon Stack receiving a SYN, test verifies that Stack sends a RST and transitions the connection to CLOSED state. <br/>
   ' - upon Stack receiving data, test verifies that Stack ignores and does not receive it. <br/>
   ' - upon Stack receiving a FIN, test verifies that Stack ignores and does not receive it. <br/>
   '*/
  CLOSINGNonConformance
   Core.EndTest
   Core.Sleep 2500

End Sub

'
' Routine to do the non conformance tests in CLOSING state
' Checks done -
'     1. RST should transition the connection to CLOSED state
'     2. SYN should result in a RST and transition the connection to CLOSED state
'     3. Send data and ensure its not received
'     4. Send a FIN and check its ignored
'
Sub CLOSINGNonConformance
   Dim ulStatus, ulSeqNumber, ulAckNumber, ulSessionId, ulTID, usSrcPort, usDestPort
   Dim ulBytesRcvd, pPacket, AutoNDObject, pIFace, PacketType, ulCounter, bResponse
   Dim ulExpectAckNumber

   ulSeqNumber = Clng(1000)
   ulAckNumber = Clng(1)
   usSrcPort = 17000
   usDestPort = 17001
   
   Set AutoNDObject = Sparta.AutoNeighbor(g_szLocalMacAddress, g_szLocalMacAddress, g_szLocalSpoofIp6Address, FALSE)    
   Set pIFace = CreateInterface(TRUE, g_szRemoteMacAddress, g_szRemoteIp6Address, g_szLocalSpoofIp6Address)
   pIFace.StartListening
   PacketType = Array(TCP_SYN, TCP_RST, TCP_ACK or TCP_PUSH, TCP_FIN or TCP_ACK)
   
   For ulCounter = lBound(PacketType) to uBound(PacketType)
      '
      ' Establish a TCP connection
      '
      Core.StartVariation
      ulStatus = ActiveConnect(usSrcPort, usDestPort, g_szLocalSpoofIp6Address, ulSeqNumber, ulAckNumber, ulSessionId, pIFace)
      if (ulStatus <> 0) then
         Core.BlockVariation "CLOSINGNonConformance: ActiveConnect - " & ulStatus
         exit sub
      end if
      
      '
      ' do loop used to get around using goto
      '
      do
         '
         ' Transition the connection to FIN_WAIT1 state
         '
         ulStatus = ESTD__FIN_WAIT1(usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId, pIFace)
         if (ulStatus <> 0) then
            Core.FailSev1Variation "CLOSINGNonConformance: ESTD__FIN_WAIT1 - " & ulStatus
            exit sub
         end if

         '
         ' Transition the connection to CLOSING state
         '
         ulStatus = FIN_WAIT1__CLOSING(usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, ulSessionId, pIFace)
         if (ulStatus <> 0) then
            Core.FailSev1Variation "CLOSINGNonConformance: FIN_WAIT1__CLOSING - " & ulStatus
            exit sub
         end if

            
         Set pPacket = MakeTCPPacket(usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, PacketType(ulCounter), 0)
         if (PacketType(ulCounter) = (TCP_ACK or TCP_PUSH)) then
            pPacket.UserData 100, "This is a test"
         end if
         pIFace.Send(pPacket)

         bResponse = TCP_ACK         
         If (PacketType(ulCounter) = TCP_SYN) then
            bResponse = TCP_RST
         end if
         
         '
         ' Wait for the response
         ' 
         Set pPacket = RecvTCPPacketEx(ulSeqNumber, ulAckNumber, ulBytesRcvd, bResponse, 200, pIFace)    
         if ((pPacket is Nothing) AND (PacketType(ulCounter)=TCP_SYN)) then
            Core.FailSev1Variation "CLOSINGNonConformance: Failed to receive a RST for SYN"
            exit do
         elseif (Not(pPacket is Nothing) AND (PacketType(ulCounter)<>TCP_SYN)) then
            Core.FailSev1Variation "CLOSINGNonConformance: Received a response"
            exit do
         end if
         
         '
         ' Ensure the connection has transitioned to CLOSED state 
         '
         if ((PacketType(ulCounter)=TCP_SYN) OR (PacketType(ulCounter)=TCP_RST)) then
            Core.StartVariation
            Set pPacket = MakeTCPPacket(usSrcPort, usDestPort, ulSeqNumber, ulAckNumber, TCP_ACK, 0)
            pIFace.Send(pPacket)
            
            Set pPacket = RecvTCPPacketEx(ulSeqNumber, ulAckNumber, ulBytesRcvd, TCP_RST, 200, pIFace)                
            if (pPacket is Nothing) then            
               Core.FailSev1Variation "CLOSINGNonConformance: Connection not in CLOSED state"
               exit do
            end if
         end if
      loop while (FALSE)

      '
      ' Reset the connection
      '
      ulStatus = ResetConnectionEx(usSrcPort, usDestPort, g_szLocalSpoofIp6Address, ulSeqNumber, ulAckNumber, ulSessionId)
      if (ulStatus <> 0) then
         Core.log "ERROR: CLOSINGNonConformance: ResetConnectionEx - " & ulStatus
      end if
   Next
End Sub

      </script>
   </job>
</package>
